apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

version = "0.9.0"

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 9
        versionName "0.9.0"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}


def siteUrl = 'https://github.com/cortinico/slidetoact'
def gitUrl = 'https://github.com/cortinico/slidetoact.git'
group = "com.ncorti"



afterEvaluate {
    publishing {
        publications {
            create("release", MavenPublication) {
                from(components["release"])
                artifact(sourcesJar)

                pom {
                    description = 'A simple \'Slide to Unlock\' Material widget for Android, written in Kotlin'
                    url = siteUrl
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'cortinico'
                            name = 'Nicola Corti'
                            email = 'corti.nico@gmail.com'
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

Properties properties = new Properties()
try {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
} catch (ignored) {
    logger.warn('File `local.properties`, not found!. If you\'re running on a CI you can ignore this warning.')
}

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'slidetoact'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
        version {
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}

ktlint {
    version = "0.39.0"
    debug = false
    verbose = true
    android = true
    outputToConsole = true
    ignoreFailures = false
    enableExperimentalRules = true
    filter {
        exclude("**/generated/**")
    }
}
